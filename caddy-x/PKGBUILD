# Maintainer: Igor Kulkov <xp.jfk9w@gmail.com>

pkgname=caddy-x
_pkgname=caddy
pkgver="${_pkgver}"
pkgrel=1
pkgdesc='HTTP/2 Web Server with Automatic HTTPS'
url='https://caddyserver.com/'
license=('Apache-2.0')
arch=('x86_64')
makedepends=('git' 'go' 'xcaddy-bin')
depends=('glibc')
source=('git+https://github.com/caddyserver/caddy.git'
        'git+https://github.com/caddyserver/dist.git'
        'caddy.sysusers'
        'caddy.tmpfiles')
sha256sums=('SKIP'
            'SKIP'
            '1dbef6bd4a096fcbab353a6bec0f6e34f8b6d2470e422c380642c2bd8ff83c20'
            '03b82d98cb8cb8376825b2b590f1a9f784d6c70f3d9cff81ff5a2ed11fa0f1c6')

provides=("${_pkgname}")
conflicts=("${_pkgname}")

backup=('etc/caddy/Caddyfile')

prepare() {
    cd "${srcdir}/caddy"
    git fetch --tags
    git checkout $(git describe --tags) 
}

pkgver() {
	cd "${srcdir}/caddy"
	echo $(git describe --tags) | sed 's/^v//g'
}

build() {
    _options="\
    --with github.com/pteich/caddy-tlsconsul \
    --with github.com/caddy-dns/porkbun \
    --with github.com/mholt/caddy-l4 \
    "

    mkdir ${srcdir}/build && cd "$_"
    xcaddy build --output ${PWD}/caddy --with github.com/caddyserver/caddy/v2=${srcdir}/caddy ${_options}

    for i in zsh bash fish; do
        ./caddy completion $i > caddy.$i
    done
}

package() {
    cd "${srcdir}"
    install -Dm644 caddy.tmpfiles "${pkgdir}/usr/lib/tmpfiles.d/caddy.conf"
    install -Dm644 caddy.sysusers "${pkgdir}/usr/lib/sysusers.d/caddy.conf"

    cd "${srcdir}/build"
    install -Dm755 caddy -t "${pkgdir}/usr/bin"
    install -Dm644 caddy.zsh "${pkgdir}/usr/share/zsh/site-functions/_caddy"
    install -Dm644 caddy.bash "${pkgdir}/usr/share/bash-completion/completions/caddy"
    install -Dm644 caddy.fish -t "${pkgdir}/usr/share/fish/vendor_completions.d"

    cd "${srcdir}/dist"
    install -Dm644 init/caddy.service -t "${pkgdir}/usr/lib/systemd/system"
    install -Dm644 init/caddy-api.service -t "${pkgdir}/usr/lib/systemd/system"
    install -Dm644 config/Caddyfile -t "${pkgdir}/etc/caddy"
    install -Dm644 welcome/index.html -t "${pkgdir}/usr/share/caddy"
}
